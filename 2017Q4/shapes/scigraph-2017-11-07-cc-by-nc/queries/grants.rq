prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sg: <http://scigraph.springernature.com/ontologies/core/>
prefix sgg: <http://scigraph.springernature.com/graphs/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix vann: <http://purl.org/vocab/vann/>
prefix void: <http://rdfs.org/ns/void#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix shapes: <http://scigraph.springernature.com/shapes/>
construct {
    ?s ?p ?o
}
where {
    {
        {
            ?s ?p ?o .
            ?s a sg:Grant .
            {
                select distinct ?s
                where{
                    ?s sg:hasFundedPublication ?article .
                    ?article sg:ddsId|sg:npgId [] .
                }
            }
            {
                select distinct ?p
                where {
                    graph sgg:shapes.grants.scigraph-2017-11-07-cc-by-nc {
                        ?shape sh:targetClass sg:Grant .
                        ?shape sh:property [ sh:path ?p ] .
                    }
                }
            }
            filter (! (?p=sg:hasFundedPublication ))
        }
        UNION
        {
            select  distinct  ?s  (sg:hasFundedPublication as ?p)   ?o
            where {
                ?s a sg:Grant .
                ?s sg:hasFundedPublication ?o.
                ?o sg:ddsId|sg:npgId [] .
                ?o sg:publicationYear ?year .
                filter(?p=sg:hasFundedPublication)
            }
        }
    }
    union
    {
        ?s a sg:Contribution .
        ?s ^sg:hasContribution ?grant .
        ?s ?p ?o .
        {
            select distinct ?grant
            where {
                ?grant sg:hasFundedPublication ?article.
                ?article sg:ddsId|sg:npgId [] .
            }
        }
        {
            select distinct ?p
            where {
                graph sgg:shapes.grants.scigraph-2017-11-07-cc-by-nc {
                    ?shape sh:targetClass sg:Contribution .
                    ?shape sh:property [ sh:path ?p ] .
                }
                filter (?p!=sg:role)
            }
        }
    }
}
