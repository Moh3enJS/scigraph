prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sg: <http://scigraph.springernature.com/ontologies/core/>
prefix sgg: <http://scigraph.springernature.com/graphs/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix vann: <http://purl.org/vocab/vann/>
prefix void: <http://rdfs.org/ns/void#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

prefix shapes: <http://scigraph.springernature.com/shapes/>
prefix product-market-codes: <http://scigraph.springernature.com/things/product-market-codes/>

construct {
    ?s ?p ?o
}
where {
    {
        {
            select distinct ?p
            where {
                graph sgg:shapes.product-market-codes.scigraph-2017-11-07-cc-by {
                    ?shape sh:targetClass skos:ConceptScheme .
                    ?shape sh:property [ sh:path ?p ] .
                }
            }
        }
        bind (product-market-codes: as ?s)
        ?s a skos:ConceptScheme .
        ?s ?p ?o .
    }
    union
    {
        {
            select distinct ?p
            where {
                graph sgg:shapes.product-market-codes.scigraph-2017-11-07-cc-by   {
                    ?shape sh:targetClass skos:Concept .
                    ?shape sh:property [ sh:path ?p ] .
                }
            }
        }
        ?s a skos:Concept .
        ?s skos:inScheme product-market-codes: .
        ?s ?p ?o .
    }
}
