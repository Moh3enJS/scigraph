prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sg: <http://www.springernature.com/scigraph/ontologies/core/>
prefix sgg: <http://www.springernature.com/scigraph/graphs/>
prefix sgo: <http://www.springernature.com/scigraph/ontologies/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix vann: <http://purl.org/vocab/vann/>
prefix void: <http://rdfs.org/ns/void#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

prefix shapes: <http://www.springernature.com/scigraph/shapes/>
prefix subjects: <http://www.springernature.com/scigraph/things/subjects/>

construct {
    ?s ?p ?o
}
where {
    {
        {
            select distinct ?p
            where {
                graph sgg:shapes.exports {
                    ?shape sh:targetClass skos:ConceptScheme .
                    ?shape sh:property [ sh:predicate ?p ] .
                }
            }
        }
        ?s a skos:ConceptScheme .
        ?s rdfs:isDefinedBy subjects: .
        ?s ?p ?o .
    }
    union
    {
        {
            select distinct ?p
            where {
                graph sgg:shapes.exports {
                    ?shape sh:targetClass skos:Concept .
                    ?shape sh:property [ sh:predicate ?p ] .
                }
            }
        }
        ?s a skos:Concept .
        ?s rdfs:isDefinedBy subjects: .
        ?s sg:isPublishReady true .
        ?s ?p ?o .
    }
}
